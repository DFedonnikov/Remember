apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: "androidx.navigation.safeargs"

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 29
    buildToolsVersion "28.0.3"
    defaultConfig {
        applicationId "com.gnest.remember"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 20
        versionName "2.0"
        setProperty("archivesBaseName", applicationName + "-v" + versionName)
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs "en", "ru"
        lintOptions { checkReleaseBuilds false }
        vectorDrawables.useSupportLibrary true
    }
    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }

        density {
            enable true
            exclude "ldpi", "tvdpi"
            compatibleScreens 'small', 'normal', 'large', 'xlarge'
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFiles fileTree('proguard').asList().toArray()
        }

        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFiles fileTree('proguard').asList().toArray()
        }
    }

    flavorDimensions "version"

    productFlavors {

        development {
            dimension "version"
            applicationIdSuffix ".development"
        }

        production {
            dimension "version"
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.2'
    }
}

androidExtensions {
    experimental = true
}

ext.abiCodes = ['armeabi-v7a': 1, 'arm64-v8a': 2, x86: 3, x86_64: 4]
ext.densityCodes = ['mdpi': 1, 'hdpi': 2, 'xhdpi': 3, 'xxhdpi': 4, 'xxxhdpi': 5]

import com.android.build.OutputFile

android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def baseAbiVersionCode = project.ext.abiCodes.get(output.getFilter(OutputFile.ABI))
        def baseDensityVersionCode = project.densityCodes.get(output.getFilter(OutputFile.DENSITY))

        if (baseAbiVersionCode != null) {
            output.versionCodeOverride = baseAbiVersionCode * 1000
        }
        if (baseDensityVersionCode != null) {
            output.versionCodeOverride += baseDensityVersionCode * 100
        }

        output.versionCodeOverride += variant.versionCode
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha4', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    //Support libraries
    implementation 'androidx.appcompat:appcompat:1.2.0-alpha03'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta4'
    implementation 'androidx.core:core:1.3.0-alpha02'
    implementation 'androidx.recyclerview:recyclerview:1.2.0-alpha01'
    implementation 'com.google.android.material:material:1.2.0-alpha05'
    implementation 'com.android.support:design:29.0.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation "androidx.lifecycle:lifecycle-livedata-core-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.2.0"

    //Kotlin
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3"

    //Navigation
    def nav_version = "2.3.0-alpha03"
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    //Lifecycle
    def lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"

    //Room
    def room_version = "2.2.4"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    //KOIN
    def koin_version = "2.0.1"
    // Koin for Android
    implementation "org.koin:koin-android:$koin_version"
    // Koin for Lifecycle scoping
    implementation "org.koin:koin-android-scope:$koin_version"
    // Koin for Android Architecture ViewModel
    implementation "org.koin:koin-android-viewmodel:$koin_version"

    implementation "dev.ahamed.mva3:adapter:3.0.0"


    // Comapact Calendar View
    implementation 'com.github.sundeepk:compact-calendar-view:2.0.2.1'

    //Mosby
    implementation 'com.hannesdorfmann.mosby3:mvp:3.1.1'
    implementation 'com.hannesdorfmann.mosby3:viewstate:3.1.1'

    //rxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.2.2'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'

    //rxBinding
    implementation 'com.jakewharton.rxbinding3:rxbinding-material:3.0.0-alpha2'

    //RxLifecycle
    implementation 'com.github.florent37:rxlifecycle:2.0.4'

    //RxPermissions
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.5@aar'

    //Realm adapters
    implementation 'io.realm:android-adapters:2.1.1'

    //BetterLinkMovementMethod
    implementation 'me.saket:better-link-movement-method:2'

    //ButterKnife
    implementation 'com.jakewharton:butterknife:10.0.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.0.0'

    //Glide
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'

    //Joda
    implementation 'joda-time:joda-time:2.10.5'

    //Crashlytics
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.1@aar') {
        transitive = true
    }

    //Leakcanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    //JUnit
    testImplementation 'junit:junit:4.12'
}
