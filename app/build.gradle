plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'androidx.navigation.safeargs'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'realm-android'
}

android {
    compileSdkVersion 33
    buildToolsVersion "30.0.2"
    defaultConfig {
        applicationId "com.gnest.remember"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 14
        versionName "1.4"
        setProperty("archivesBaseName", applicationName + "-v" + versionName)
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs "en", "ru"
        lintOptions { checkReleaseBuilds false }
        vectorDrawables.useSupportLibrary true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation":"$projectDir/schemas".toString(),
                        "room.incremental":"true",
                        "room.expandProjection":"true"]
            }
        }
    }
    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }

        density {
            enable true
            exclude "ldpi", "tvdpi"
            compatibleScreens 'small', 'normal', 'large', 'xlarge'
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFiles fileTree('proguard').asList().toArray()
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFiles fileTree('proguard').asList().toArray()
        }
    }

    flavorDimensions "version"

    productFlavors {

        development {
            dimension "version"
            applicationIdSuffix ".development"
        }

        production {
            dimension "version"
        }

    }

    compileOptions {
        sourceCompatibility 11
        targetCompatibility 11
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.2'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.3.2"
    }
}

androidExtensions {
    experimental = true
}

ext.abiCodes = ['armeabi-v7a': 1, 'arm64-v8a': 2, x86: 3, x86_64: 4]
ext.densityCodes = ['mdpi': 1, 'hdpi': 2, 'xhdpi': 3, 'xxhdpi': 4, 'xxxhdpi': 5]

import com.android.build.OutputFile

android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def baseAbiVersionCode = project.ext.abiCodes.get(output.getFilter(OutputFile.ABI))
        def baseDensityVersionCode = project.densityCodes.get(output.getFilter(OutputFile.DENSITY))

        if (baseAbiVersionCode != null) {
            output.versionCodeOverride = baseAbiVersionCode * 1000
        }
        if (baseDensityVersionCode != null) {
            output.versionCodeOverride += baseDensityVersionCode * 100
        }

        output.versionCodeOverride += variant.versionCode
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha4', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    //Hilt
    implementation "com.google.dagger:hilt-android:2.44"
    kapt "com.google.dagger:hilt-compiler:2.44"

    //Room
    def room_version = "2.4.3"

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    //Compose
    implementation 'androidx.compose.material3:material3:1.0.0-rc01'
    implementation 'androidx.activity:activity-compose:1.6.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1'
    implementation 'androidx.compose.ui:ui-tooling-preview:1.2.1'
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
    debugImplementation 'androidx.compose.ui:ui-tooling:1.2.1'

    //Support libraries
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0-rc01'
//    implementation 'com.google.android.material:material:1.7.0'
//    implementation 'com.android.support:design:28.0.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'

    //Navigation
    def nav_version = "2.5.2"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    // For Kotlin use navigation-fragment-ktx
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    // For Kotlin use navigation-ui-ktx

    // Comapact Calenar View
    implementation 'com.github.sundeepk:compact-calendar-view:2.0.2.1'

    //Mosby
    implementation 'com.hannesdorfmann.mosby3:mvp:3.1.1'
    implementation 'com.hannesdorfmann.mosby3:viewstate:3.1.1'

    //rxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.2.2'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'

    //rxBinding
    implementation 'com.jakewharton.rxbinding3:rxbinding-material:3.0.0-alpha2'

    //RxLifecycle
    implementation 'com.github.florent37:rxlifecycle:2.0.4'

    //RxPermissions
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.5@aar'

    //Realm adapters
    implementation 'io.realm:android-adapters:2.1.1'

    //BetterLinkMovementMethod
    implementation 'me.saket:better-link-movement-method:2'

    //ButterKnife
    implementation 'com.jakewharton:butterknife:10.0.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.0.0'

    //Glide
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'

    //Firebase
    implementation 'com.google.firebase:firebase-crashlytics:18.3.1'
    implementation 'com.google.firebase:firebase-analytics:21.2.0'

    //Kotlin date time
    implementation 'org.jetbrains.kotlinx:kotlinx-datetime:0.4.0'


//    //Leakcanary
//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'

    //JUnit
    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test:core:1.4.0'

    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4"
}

kapt {
    correctErrorTypes true
}
